plugins {
    id 'base'
    id 'com.github.blindpirate.gogradle' version '0.11.4'
    id 'com.bmuschko.docker-remote-api' version '4.8.1'
}

ext {
    binaryFullPath = "$buildDir/bin/dse-\${PROJECT_NAME}-\${GOOS}-\${GOARCH}"
}

golang {
    packagePath = 'github.com/riptano/dse-operator/operator'
    goVersion = '1.12.5'
    goExecutable = 'IGNORE_LOCAL'
}
import com.github.blindpirate.gogradle.crossplatform.Arch
import com.github.blindpirate.gogradle.crossplatform.Os

goBuild {
    targetPlatform = [ "${Os.hostOs}-${Arch.hostArch}", 'linux-amd64']*.toString()
    go (["build", '-o', binaryFullPath, "-ldflags", "-X main.version=$version", "${golang.packagePath}/cmd/manager"]*.toString())
    outputLocation = binaryFullPath
}

check.dependsOn goTest

import com.bmuschko.gradle.docker.tasks.image.*

task copyDockerFiles(type: Copy) {
    from 'docker'
    into buildDir
}

task dockerImage(type: DockerBuildImage, group: 'Docker', description: 'Build operator Docker image') {
    dependsOn 'buildLinuxAmd64', copyDockerFiles
    inputDir = buildDir
    tags.add("datastax/dse-${project.name}:${version.toString().replaceAll('[^a-zA-Z0-9\\.\\-_]', '_')}")
}

// "go mod vendor" is necessary to provide IDE autocompletion
// and also for the operator-sdk code generation.
task runGoModVendor(type: Exec) {
    environment "GO111MODULE", "on"
    commandLine 'go', 'mod', 'vendor'
}

assemble.dependsOn goBuild, dockerImage

[resolveBuildDependencies, installDependencies]*.enabled = false

import com.github.blindpirate.gogradle.Go

tasks.matching { (it.name.startsWith('build') && it instanceof Go ) || it.name == 'goTest' }.all {
    environment 'GO111MODULES': 'on', 'CGO_ENABLED': '0'
    inputs.file 'go.mod'
}

[resolveBuildDependencies, installDependencies, resolveTestDependencies]*.enabled = false

task upLocal(type: LocalOperatorStart, group: 'Run', description: 'Run the operator locally against the configured k8s cluster')

gradle.projectsEvaluated {
    tasks.matching { it.name == "build${Os.hostOs.toString().capitalize()}${Arch.hostArch.toString().capitalize()}"}.all { t ->
        upLocal {
            dependsOn t
            commandLine t.outputs.files.singleFile
        }
    }
}

class LocalOperatorStart extends Exec {

    LocalOperatorStart() {
        environment.WATCH_NAMESPACE = 'default'
    }

    @Option(option="namespace", description="k8s namespace to watch for events, default to 'default'")
    void setNamespace(String ns) {
        environment.WATCH_NAMESPACE = ns
    }
}

task createOperatorSdkDockerImage(type: Exec) {
    workingDir = "../tools/operator-sdk"
    commandLine 'docker', 'build', '-t', 'operator-sdk-binary', '.'
}

// This is needed for operator-sdk generate k8s to run
task mkdirBuild(type: Exec) {
    commandLine 'mkdir', '-p', 'build'
}

// This is needed for operator-sdk generate k8s to run
task touchBuildDockerfile(type: Exec) {
    commandLine 'touch', 'build/Dockerfile'
}

// Internal task to generate the files and clean up afterwards
task doUpdateGeneratedFiles(type: Exec) {
    workingDir = ".."
    // Note: the temporary build artifacts in operator/build/_output may be
    // owned by root:root. No matter what the ownership is, the docker image
    // has permission to remove them, so we remove "build"
    commandLine 'docker', 'run', '--rm', '-t', '-v', "$rootProject.projectDir:/go/src/github.com/riptano/dse-operator", "operator-sdk-binary", "/bin/bash", '-c', 'export GO111MODULE=on; cd ../../riptano/dse-operator/operator && operator-sdk generate k8s && operator-sdk generate openapi && rm -rf build'
}

// This task is manually tweak the crd.yaml file
task updateGeneratedFiles(type: Exec) {
    workingDir = ".."
    // A perl one-liner seems to be more portable and readable than sed/awk solutions
    // Remove lines between config: and type:
    commandLine 'perl', '-i', '-ne', 'print unless /config:/ .. /type:/', 'operator/deploy/crds/datastax_v1alpha1_dsedatacenter_crd.yaml'
}

touchBuildDockerfile.dependsOn mkdirBuild
doUpdateGeneratedFiles.dependsOn createOperatorSdkDockerImage, runGoModVendor, touchBuildDockerfile
updateGeneratedFiles.dependsOn doUpdateGeneratedFiles
